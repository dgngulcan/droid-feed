apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

ext.app = [
        'version'            : '2.3.0',
        'version_code'       : 26,
        'compile_sdk_version': 29,
        'min_sdk_version'    : 21,
        'target_sdk_version' : 29,
        'build_tools_version': '29.0.2'
]

android {
    compileSdkVersion app.compile_sdk_version
    buildToolsVersion app.build_tools_version

    defaultConfig {
        applicationId "com.droidfeed"
        minSdkVersion project.app.min_sdk_version
        targetSdkVersion project.app.target_sdk_version
        versionName project.app.version
        versionCode project.app.version_code
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', "DROIDFEED_GITHUB_URL", "\"https://github.com/dgngulcan/droid-feed\""
        buildConfigField 'String', "DROIDFEED_PRIVACY_POLICY", "\"https://app.termly.io/document/privacy-policy/ab750f11-7f6d-499d-aafb-57697bd8aba0\""

        multiDexEnabled true
        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding.enabled = true

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        ignoreWarnings true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.material_design
    implementation deps.androidx.core_ktx
    implementation deps.androidx.fragment
    implementation deps.androidx.browser
    implementation deps.androidx.lifecycle
    implementation deps.androidx.lifecycle_ktx
    implementation deps.androidx.livedata_ktx
    implementation deps.androidx.viewmodel_ktx
    kapt deps.androidx.lifecycle_compiler
    implementation deps.androidx.paging
    implementation deps.androidx.constraint_layout
    implementation deps.androidx.swipe_refresh_layout
    implementation deps.androidx.room_runtime
    kapt deps.androidx.room_compiler
    implementation deps.dagger.core
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.processor
    implementation deps.glide.core
    kapt deps.glide.compiler
    implementation deps.retrofit.core
    implementation deps.retrofit.moshi_converter
    implementation deps.okhttp.core
    implementation deps.okhttp.logging_interceptor
    implementation deps.crashlytics
    implementation deps.lottie
    implementation deps.firebase.analytics
    implementation deps.firebase.config
    implementation deps.firebase.firestore
    implementation deps.jsoup
    testImplementation deps.mockk
    testImplementation deps.junit
    testImplementation deps.kotlin.coroutines_test
    testImplementation deps.androidx.test_core
    testImplementation deps.arch.testing
    androidTestImplementation deps.arch.testing
    androidTestImplementation deps.androidx.test_core
    androidTestImplementation deps.androidx.test_runner
    androidTestImplementation deps.androidx.test_espresso
    androidTestImplementation deps.androidx.test_rules
    androidTestImplementation deps.androidx.test_monitor
    androidTestImplementation deps.androidx.test_ext
    androidTestImplementation deps.androidx.room_testing

    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}

apply plugin: 'com.google.gms.google-services'